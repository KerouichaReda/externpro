# openssl
set(OPENSSL_OLDVER 1.0.2a)
set(OPENSSL_NEWVER 1.1.1c)
########################################
function(build_openssl)
  string(TOUPPER ${OPENSSL_OLDVER} OLDVER)
  string(TOUPPER ${OPENSSL_NEWVER} NEWVER)
  if(NOT (XP_DEFAULT OR XP_PRO_OPENSSL_${OLDVER} OR XP_PRO_OPENSSL_${NEWVER}))
    return()
  endif()
  if(XP_DEFAULT)
    set(OPENSSL_VERSIONS ${OPENSSL_OLDVER} ${OPENSSL_NEWVER})
  else()
    if(XP_PRO_OPENSSL_${OLDVER})
      set(OPENSSL_VERSIONS ${OPENSSL_OLDVER})
    endif()
    if(XP_PRO_OPENSSL_${NEWVER})
      list(APPEND OPENSSL_VERSIONS ${OPENSSL_NEWVER})
    endif()
  endif()
  list(REMOVE_DUPLICATES OPENSSL_VERSIONS)
  list(LENGTH OPENSSL_VERSIONS NUM_VER)
  if(NUM_VER EQUAL 1)
    set(ONE_VER "set(XP_USE_LATEST_OPENSSL ON) # currently only one version supported\n")
  endif()
  set(MOD_OPT "set(VER_MOD)")
  set(VER_CFG xpConfigBase)
  set(USE_SCRIPT_INSERT ${ONE_VER}${MOD_OPT})
  configure_file(${PRO_DIR}/use/usexp-openssl-config.cmake ${STAGE_DIR}/share/cmake/
    @ONLY NEWLINE_STYLE LF
    )
  foreach(ver ${OPENSSL_VERSIONS})
    xpCmakeBuild(openssl_${ver} "" "-DOPENSSL_VER=${ver}" opensslTargets_${ver})
    list(APPEND opensslTargets ${opensslTargets_${ver}})
  endforeach()
  if(ARGN)
    set(${ARGN} "${opensslTargets}" PARENT_SCOPE)
  endif()
endfunction()
